#version 460
#extension GL_GOOGLE_include_directive : require
#extension GL_EXT_nonuniform_qualifier : require
#extension GL_EXT_ray_query : require

#include "Platform.glsl"
#include "common/Material.glsl"
#include "common/UniformBufferObject.glsl"
#include "common/Random.glsl"
#include "common/GGXSample.glsl"

layout(binding = 0, rg32ui) uniform uimage2D MiniGBuffer0;
layout(binding = 2, rgba8) uniform image2D OutImage;
layout(binding = 3) readonly uniform UniformBufferObjectStruct { UniformBufferObject Camera; };
layout(binding = 4) readonly buffer VertexArray { float Vertices[]; };
layout(binding = 5) readonly buffer IndexArray { uint Indices[]; };
layout(binding = 6) readonly buffer MaterialArray { Material[] Materials; };
layout(binding = 7) readonly buffer OffsetArray { uvec2[] Offsets; };
layout(binding = 8) readonly buffer NodeProxyArray { NodeProxy[] NodeProxies; };
layout(binding = 9, rg16f) uniform image2D OutMotionVector;
layout(binding = 10, set = 0) uniform accelerationStructureEXT Scene;
layout(binding = 13, rgba16f) uniform image2D OutAlbedoBuffer;
layout(binding = 14, rgba16f) uniform image2D OutNormalBuffer;
layout(binding = 16) buffer AmbientCubeArray { AmbientCube[] Cubes; };
layout(binding = 17) readonly buffer HDRSHArray { SphericalHarmonics[] HDRSHs; };
layout(binding = 18) readonly buffer LightObjectArray { LightObject[] Lights; };

layout(set = 1, binding = 0) uniform sampler2D TextureSamplers[];

#include "common/Vertex.glsl"
#include "common/Const_Func.glsl"
#include "common/SampleIBL.glsl"
#include "common/RTSimple.glsl"
#include "common/AmbientCube.glsl"

#if DESKTOP
layout(local_size_x = 8, local_size_y = 4, local_size_z = 1) in;
#else
layout(local_size_x = 32, local_size_y = 32, local_size_z = 1) in;
#endif

//-----------------------------------------------------------------------------
struct RayPayload
	//-----------------------------------------------------------------------------
{
	// Flag for hit or miss
	uint    IsHit;

	// Geometry instance ids
	int     PrimitiveIndex;
	int     InstanceID;
	int     InstCustIndex;
	// in     int   gl_GeometryIndexEXT;

	// World space parameters
	vec3   RayOrigin;
	vec3   RayDirection;

	// Ray hit info
	float  HitDist;
	bool   IsFrontFacing;

	// Barycentric Coordinates
	vec3    BaryCoords;
};

uint FetchMaterialId(in NodeProxy node, uint offset)
{
	uint materialIndex = 0;
	switch (offset) {
		case 0:
			materialIndex = node.matId[0];
			break;
		case 1:
			materialIndex = node.matId[1];
			break;
		case 2:
			materialIndex = node.matId[2];
			break;
		case 3:
			materialIndex = node.matId[3];
			break;
		case 4:
			materialIndex = node.matId[4];
			break;
		case 5:
			materialIndex = node.matId[5];
			break;
		case 6:
			materialIndex = node.matId[6];
			break;
		case 7:
			materialIndex = node.matId[7];
			break;
		case 8:
			materialIndex = node.matId[8];
			break;
		case 9:
			materialIndex = node.matId[9];
			break;
		case 10:
			materialIndex = node.matId[10];
			break;
		case 11:
			materialIndex = node.matId[11];
			break;
		case 12:
			materialIndex = node.matId[12];
			break;
		case 13:
			materialIndex = node.matId[13];
			break;
		case 14:
			materialIndex = node.matId[14];
			break;
		case 15:
			materialIndex = node.matId[15];
			break;
	}
	return materialIndex;
}

bool TraceRay(ivec2 ipos, vec3 origin, vec3 direction, vec3 iblColor, inout vec3 bounceColor, inout vec3 illumColor)
{
	rayQueryEXT rayQuery;
	rayQueryInitializeEXT(rayQuery, Scene, gl_RayFlagsNoneEXT, 0xFF, origin, EPS2, direction, 20.f);
	rayQueryProceedEXT(rayQuery);
	while( rayQueryProceedEXT(rayQuery) )
	{

	}
//	rayQueryInitializeEXT(rayQuery, Scene, gl_RayFlagsTerminateOnFirstHitEXT, 0xFF, origin, EPS, direction, 20.0f);
//	rayQueryProceedEXT(rayQuery);

	if (rayQueryGetIntersectionTypeEXT(rayQuery, true) == gl_RayQueryCommittedIntersectionTriangleEXT  ) {
		RayPayload PayloadData;

		const bool IsCommitted = true;

		PayloadData.PrimitiveIndex = rayQueryGetIntersectionPrimitiveIndexEXT(rayQuery, IsCommitted);
		PayloadData.InstanceID = rayQueryGetIntersectionInstanceIdEXT(rayQuery, IsCommitted);

		// Ray hit info
		PayloadData.HitDist = rayQueryGetIntersectionTEXT(rayQuery, IsCommitted);
		PayloadData.IsFrontFacing = rayQueryGetIntersectionFrontFaceEXT(rayQuery, IsCommitted);

		mat4x3 WorldToObject = rayQueryGetIntersectionWorldToObjectEXT(rayQuery, IsCommitted);
		
		vec2 TwoBaryCoords = rayQueryGetIntersectionBarycentricsEXT(rayQuery, IsCommitted);
		PayloadData.BaryCoords = vec3(1.0 - TwoBaryCoords.x - TwoBaryCoords.y, TwoBaryCoords.x, TwoBaryCoords.y);

		const NodeProxy node = NodeProxies[PayloadData.InstanceID];
		const uvec2 offsets = Offsets[node.modelId];
		const uint indexOffset = offsets.x + PayloadData.PrimitiveIndex * 3;
		const uint vertexOffset = offsets.y;
		const Vertex v0 = UnpackVertex(vertexOffset + Indices[indexOffset]);
		const Vertex v1 = UnpackVertex(vertexOffset + Indices[indexOffset + 1]);
		const Vertex v2 = UnpackVertex(vertexOffset + Indices[indexOffset + 2]);
		uint matIdx = FetchMaterialId(node, v0.MaterialIndex);
		const Material material = Materials[matIdx];
		const vec3 normal = normalize((Mix(v0.Normal, v1.Normal, v2.Normal, PayloadData.BaryCoords) * WorldToObject).xyz);

		//const vec2 texCoord = Mix(v0.TexCoord, v1.TexCoord, v2.TexCoord, PayloadData.BaryCoords);
		//const vec4 texColor = material.DiffuseTextureId >= 0 ? texture(TextureSamplers[nonuniformEXT(material.DiffuseTextureId)], texCoord) : vec4(1);
		//const vec4 lightColor = material.MaterialModel == MaterialDiffuseLight ? material.Diffuse : vec4(0);
		//illumColor = lightColor.rgb;
		//const vec3 albedo = material.MaterialModel == MaterialDiffuseLight ? vec3(0) : texColor.rgb * texColor.rgb * material.Diffuse.rgb;
		const vec3  albedo = material.MaterialModel == MaterialDiffuseLight ? vec3(0) : material.Diffuse.rgb;

		vec3 hitPos = origin + direction * PayloadData.HitDist;
		vec3 localPos = (hitPos - CUBE_OFFSET) / CUBE_UNIT;
		// 这里都是hit了，那就走间接光照，直接用hitpos和normal，从AmbientCube里取
		vec4 indirectFromAmbientCube = Camera.FastInterpole ? vec4(0) : interpolateProbes(localPos, normal);
		bounceColor = albedo * indirectFromAmbientCube.rgb;
		
		return true;
	}
	// miss, hit sky
	return false;
}

#include "common/VertexFunc.glsl"

void main() {
	int adder = Camera.TotalFrames % 2 == 0 ? 1 : 0;
		
    // checker box
    ivec2 ipos = ivec2(gl_GlobalInvocationID.xy);
	
	ivec2 size = imageSize(MiniGBuffer0);
    uvec2 vBuffer = imageLoad(MiniGBuffer0, ipos).rg;
    vec2 uv = vec2(ipos) / vec2(size) * 2.0 - 1.0;
    vec4 origin = Camera.ModelViewInverse * vec4(0, 0, 0, 1);
	vec4 target = Camera.ProjectionInverse * (vec4(uv.x, uv.y, 1, 1));
	vec4 dir = Camera.ModelViewInverse * vec4(normalize(target.xyz), 0);
	vec3 ray_dir = normalize(dir.xyz);
	
	// x == y == 0, hit the sky, quick go
	if(vBuffer.x == 0)
	{
		vec3 iblColor = Camera.HasSky ? SampleIBL(Camera.SkyIdx, ray_dir, Camera.SkyRotation, 0).rgb * Camera.SkyIntensity : vec3(0.0);
		imageStore(OutImage, ipos, vec4(iblColor,1));
		imageStore(OutMotionVector, ipos, vec4(0));
		imageStore(OutAlbedoBuffer, ipos, vec4(iblColor,1.0));
		imageStore(OutNormalBuffer, ipos, vec4(0,1,0,1));
		return;
	}

	uvec4 RandomSeed = InitRandomSeed(ipos.x, ipos.y, Camera.TotalFrames);
    
	// visibility fetch hit point, if primary ray, start from here
    Vertex v = get_material_data(ipos, vBuffer, origin.xyz, ray_dir);
	NodeProxy node = NodeProxies[vBuffer.x - 1];
	uint materialIndex = FetchMaterialId(node, v.MaterialIndex);
	Material mat = Materials[materialIndex];
    vec4 albedo = mat.Diffuse;
    if (mat.DiffuseTextureId >= 0)
    {
        vec4 tex = texture(TextureSamplers[mat.DiffuseTextureId], v.TexCoord);
        albedo *= tex * tex;
    }
	
	vec3 normal = normalize( v.Normal.rgb);
	
	// ibl
	const float dotValue = dot(ray_dir, normal);
	const vec3 outwardNormal = dotValue > 0 ? -normal : normal;
	const float cosine = dotValue > 0 ? mat.RefractionIndex * dotValue : -dotValue;
	const float reflectProb = Schlick(cosine, mat.RefractionIndex);
	const float metalProb = mat.Metalness;
	
	
	vec3 irradianceColor = vec3(0);
	vec3 bounceColor = vec3(0);
	uint sampleTimes = Camera.FastGather ? 1 : 4;

	if (mat.MaterialModel == MaterialDiffuseLight)
	{
		vec3 lightColor = albedo.rgb;
		imageStore(OutImage, ipos, vec4(lightColor,1));
		imageStore(OutMotionVector, ipos, vec4(0));
		imageStore(OutAlbedoBuffer, ipos, vec4(lightColor,1.0));
		imageStore(OutNormalBuffer, ipos, vec4(normal,1.0));
		return;
	}

	// calculate the motion vector
	vec4 currFrameHPos = Camera.ViewProjection * vec4(v.Position, 1);
	vec2 currfpos = vec2((currFrameHPos.xy / currFrameHPos.w * 0.5) * vec2(size));

	vec4 prevFrameHPos = Camera.PrevViewProjection * node.combinedPrevTS * vec4(v.Position, 1);
	vec2 prevfpos = vec2((prevFrameHPos.xy / prevFrameHPos.w * 0.5) * vec2(size));
	vec2 motion = prevfpos - currfpos;
	imageStore(OutMotionVector, ipos, vec4(motion,0,0));

	// emit ray without any guidance
	for (uint s = 0; s < sampleTimes; ++s)
	{
	    bool chanceReflect = bool(RandomFloat(RandomSeed) < reflectProb);
		bool chanceMetal = bool(RandomFloat(RandomSeed) < metalProb);
		bool chanceGGX = chanceReflect || chanceMetal;
		const vec3 trace_next = chanceGGX ? reflect( ray_dir, outwardNormal) : outwardNormal;
		const vec3 trace_dir = chanceGGX ? ggxSampling(RandomSeed, sqrt(mat.Fuzziness), trace_next) : AlignWithNormal( RandomInHemiSphere1(RandomSeed), trace_next );

		vec3 posOffset = normal * 0.01;
		if(mat.MaterialModel == MaterialDielectric)
		{
			//albedo = vec4(1,1,1,1);
			//posOffset = ray_dir * 0.1;
		}

		// if miss, sample the sky
		vec3 bounceSingle = vec3(0);
		vec4 albedoSingle = albedo;
		
		if(chanceReflect && mat.MaterialModel == MaterialMixture)
		{
			albedoSingle = vec4(1);
		}
		
		#if DESKTOP
		vec3 iblColor = Camera.HasSky ? SampleIBL(Camera.SkyIdx, trace_dir, Camera.SkyRotation, 0).rgb * Camera.SkyIntensity : vec3(0.0);
		#else
		vec3 iblColor = Camera.HasSky ? SampleIBL(Camera.SkyIdx, trace_dir, Camera.SkyRotation, mat.Fuzziness).rgb * Camera.SkyIntensity : vec3(0.0);
		#endif
		vec3 illumColor = vec3(0.0);
		
		bool hit = true;
		
		if(Camera.FastGather)
		{
			if((gl_GlobalInvocationID.y + adder) % 2 == 0)
			{
				hit = TraceRay(ipos, v.Position + posOffset, trace_dir, iblColor, bounceSingle, illumColor);
				iblColor *= 2.0f;
				bounceSingle *= 2.0f;
			}
		}
		else
		{
			hit = TraceRay(ipos, v.Position + posOffset, trace_dir, iblColor, bounceSingle, illumColor);
		}
		
		if(!hit)
		{
			irradianceColor += albedoSingle.rgb * iblColor;
		}
		else
		{
			bounceColor += albedoSingle.rgb * bounceSingle * 1.25f; // 这是一个经验值，理论上，应该为1.0，但是因为hybird方式缺少反弹，因此将这个值拉大可以增强二次反弹亮度，pbr场景，2.0刚好符合PT结果
		}
	}

	irradianceColor = irradianceColor / sampleTimes;
	bounceColor = bounceColor / sampleTimes;

    vec4 outColor = vec4(irradianceColor,1);
	outColor.a = 1.0;//sampleTimes / sampleTimes;
    
    if(Camera.HasSun)
    {
    	const vec3 lightVector = Camera.SunDirection.xyz;
		float ndotl = clamp(dot(normal, lightVector), 0.0f, 1.0f);
		vec3 d = Camera.SunColor.xyz * ndotl * 0.25;
        
        d = mix(d, vec3(0.0), vec3(metalProb));
    
        const vec3 lightVectorCone = AlignWithNormal( RandomInCone(RandomSeed, cos(0.25f / 180.f * M_PI)), lightVector);

        rayQueryEXT rayQuery;
        rayQueryInitializeEXT(rayQuery, Scene, gl_RayFlagsTerminateOnFirstHitEXT, 0xFF, v.Position.xyz, EPS, lightVectorCone, INF);
        rayQueryProceedEXT(rayQuery);
        if (rayQueryGetIntersectionTypeEXT(rayQuery, true) == gl_RayQueryCommittedIntersectionTriangleEXT  ) {
            d = vec3(0.0);
        }
        outColor.rgb += albedo.rgb * d;
    }

	if(Camera.LightCount > 0)
	{
		LightObject light = Lights[0];
		vec3 lightPos = light.p0.xyz + (light.p1.xyz - light.p0.xyz) * RandomFloat(RandomSeed) + (light.p3.xyz - light.p0.xyz) *  RandomFloat(RandomSeed);
		float lightAtten = TracingOccludeFunction(v.Position, lightPos);

		vec3 lightDir = normalize(lightPos - v.Position);
		float ndotl = sqrt(clamp(dot(normal, lightDir), 0.0f, 1.0f));
		float distance = length(lightPos - v.Position);
		float attenuation = 1.0f / (distance * distance);
		outColor.rgb += albedo.rgb * vec3(2000.0f, 2000.0f, 2000.0f) * ndotl * attenuation * lightAtten;
	}
	
	vec4 directLight = vec4(outColor.rgb, 1);
	
	imageStore(OutAlbedoBuffer, ipos, albedo);
	imageStore(OutNormalBuffer, ipos, vec4(normal,1.0));
	// with multiple bounceColor
	outColor.rgb += bounceColor.rgb;

	//#if USE_FIREFLY_FILTER
 	 float lum = luminance(outColor.rgb);
	if(lum > 1000.0F)
	{
		outColor.rgb *= 1000.0F / lum;
	}
	//#endif
	
    imageStore(OutImage, ipos, outColor);
}