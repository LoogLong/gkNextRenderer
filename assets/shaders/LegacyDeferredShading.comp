#version 460
#extension GL_GOOGLE_include_directive : require
#extension GL_EXT_nonuniform_qualifier : require
#extension GL_EXT_shader_image_load_formatted : require

#include "common/Const_Func.glsl"
#include "common/UniformBufferObject.glsl"

layout(binding = 0) readonly uniform image2D GBuffer0Image;
layout(binding = 1) readonly uniform image2D GBuffer1Image;
layout(binding = 2) readonly uniform image2D GBuffer2Image;
layout(binding = 3) writeonly uniform image2D OutImage;
layout(binding = 4) readonly uniform UniformBufferObjectStruct { UniformBufferObject Camera; };
layout(binding = 7) readonly buffer HDRSHArray { SphericalHarmonics[] HDRSHs; };
layout(binding = 8) uniform sampler2D ShadowMapSampler;

layout(set = 1, binding = 0) uniform sampler2D TextureSamplers[];

layout(local_size_x = 8, local_size_y = 4, local_size_z = 1) in;

float getShadow(vec3 worldPos, vec3 jit, vec3 normal, ivec2 ipos) {
    // 计算光源空间坐标
    vec4 posInLightMap = Camera.SunViewProjection * vec4(worldPos + jit * 4.0f, 1.0f);

    // 将光源空间坐标转换到NDC空间 [-1,1] 再转到 [0,1] 的纹理空间
    vec3 projCoords = posInLightMap.xyz / posInLightMap.w;
    projCoords = projCoords * 0.5 + 0.5;
    projCoords.y = 1.0 - projCoords.y;

    float currentDepth = projCoords.z;

    float bias = 0.0005;
    
    float cosTheta = max(dot(normal, normalize(Camera.SunDirection.xyz)), 0.0);
    bias = mix(0.0001, 0.00005, cosTheta);
    
    float closestDepth = texture(ShadowMapSampler, projCoords.xy).x;
    float shadow = currentDepth - bias > closestDepth ? 0.0 : 1.0;

    return shadow;
}

void main() {

    ivec2 ipos = ivec2(gl_GlobalInvocationID.xy);

    vec4 albedo = imageLoad(GBuffer0Image, ipos);
    vec4 normalraw = imageLoad(GBuffer1Image, ipos);
    vec4 pbr_param = imageLoad(GBuffer2Image, ipos);
    
    vec3 normal = normalize(normalraw.rgb);

    const float t = 0.5*(normal.y + 1);
    vec4 iblLight = vec4(Camera.SkyIntensity);//Camera.HasSky ? SampleIBL(Camera.SkyIdx, normal, Camera.SkyRotation, 1) * Camera.SkyIntensity : vec4(0.0);
    
    const vec3 lightVector = Camera.SunDirection.xyz;
    const vec4 d = Camera.SunColor * max(dot(lightVector, normal), 0.0) * M_1_PI;

    float shadow = 0.0f;
    if(Camera.HasSun)
    {
        vec3 jit = vec3(0);
        shadow += getShadow(pbr_param.xyz, jit, normal, ipos);
    }

    if( Camera.DebugDraw_Lighting )
    {
        albedo = vec4(0.5,0.5,0.5,1);
    }
    
    vec4 outColor = albedo * d * shadow + iblLight * albedo;
    
    if(Camera.HDR)
    {
        imageStore(OutImage, ipos, vec4( LinearToST2084UE( outColor.rgb * Camera.PaperWhiteNit / 230.0), 1.0));
    }
    else
    {
        imageStore(OutImage, ipos , vec4( Uncharted2_Tonemapping( outColor.rgb * Camera.PaperWhiteNit / 20000.0), 1.0));
    }
}